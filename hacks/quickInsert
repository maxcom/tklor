#
# Расширение, добавляющее возможность использовать шаблоны для быстрого наполнения сообщений.
#
# Использование:
#  Добавьте в файл ~/.config/tkLOR/userConfig (или создайте его, если он отсутствует)
#  строку
#    loadPlugin "/путь/к/этому/файлу"
#  Путь можно указывать как абсолютный, так и относительно текущего каталога.
#  Обращаться к переменным окружения можно через $env(VARIABLE), например:
#    loadPlugin "$env(HOME)/tklor-addons/quickInsert"
#

############################################################################
#   Copyright (C) 2008 Alexander Galanin <gaa.nnov@mail.ru>                #
#                                                                          #
#   This program is free software: you can redistribute it and/or modify   #
#   it under the terms of the GNU General Public License as published by   #
#   the Free Software Foundation, either version 3 of the License, or      #
#   (at your option) any later version.                                    #
#                                                                          #
#   This program is distributed in the hope that it will be useful,        #
#   but WITHOUT ANY WARRANTY; without even the implied warranty of         #
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the          #
#   GNU General Public License for more details.                           #
#                                                                          #
#   You should have received a copy of the GNU General Public License      #
#   along with this program.  If not, see <http://www.gnu.org/licenses/>.  #
############################################################################

package require getstring

namespace eval user {
namespace eval quickInsert {

if { ![ info exists ::__user_quickInsert_texts ] } {
    set ::__user_quickInsert_texts {
        "А что скажет Патрег?"
        "Тормозное быдлоподелие!"
        "Убей себя как Томми!"
        "В биореактор, быдло!"
        "Где же гик?"
        "Жава не тормозит!!111"
        "<> не нужно"
        "Ждём ебилдов"
        "У меня всё работает. ЧЯДНТ?"
    }
}

set opt $::options
set ::options {}
foreach {t v} $opt {
    if { $t == "Global options" } {
        lappend v \
            "Быстрая вставка" \
            hidden \
            __user_quickInsert_texts \
            {}
    }
    lappend ::options $t $v
}

proc customize {parent} {
    set w [ toplevel .quickInsertCustomizer -class Dialog ]
    wm withdraw $w
    wm title $w "Настройка быстрой вставки"

    set listw [ listbox $w.list -listvariable __user_quickInsert_texts ]
    grid $listw -sticky nswe -padx 2 -pady 2

    set f [ ttk::frame $w.buttons ]
    grid $f -sticky nswe

    grid \
        [ ttk::button $f.add \
            -command [ ::lambda::lambda {} {
                set ::user::quickInsert::tempVar {}

                if [ ::getstring::tk_getString .quickInsertCustomizer.getStringWindow \
                        ::user::quickInsert::tempVar \
                        "Введите новый шаблон"
                ] {
                    lappend ::__user_quickInsert_texts $::user::quickInsert::tempVar
                }
            } ] \
            -text "Добавить" \
        ] \
        [ ttk::button $f.remove \
            -command [ ::lambda::closure {listw} {} {
                if { [ $listw curselection ] != "" } {
                    $listw delete [ $listw curselection ]
                }
            } ] \
            -text "Удалить" \
        ] \
        [ ttk::button $f.close \
            -command "destroy $w" \
            -text "Закрыть" \
        ] \
        -padx 2 \
        -pady 2 \
        -sticky nswe

    grid columnconfigure $w 0 -weight 1
    grid rowconfigure $w 0 -weight 1

    wm deiconify $w
    wm transient $w $parent
    update
    grab $w
}

}
}

rename ::mailEditor::makeContextMenu ::user::quickInsert::makeContextMenu_orig

proc makeContextMenu {window textWidget} {
    upvar #0 __user_quickInsert_texts texts

    set menu [ ::user::quickInsert::makeContextMenu_orig $window $textWidget ]

    set m [ menu $menu.quickInsertMenu -tearoff 0 ]
    foreach t $texts {
        if { [ string length $t ] > 20 } {
            set tt "[ string range $t 0 20 ]..."
        } else {
            set tt $t
        }
        $m add command \
            -label $tt \
            -command [ list $textWidget insert insert $t ]
    }
    $m add separator
    $m add command \
        -label "Настроить..." \
        -command [ list ::user::quickInsert::customize $textWidget ]

    $menu add separator
    $menu add cascade \
        -label "Быстрая вставка" \
        -menu $m

    return $menu
}

