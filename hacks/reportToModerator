#
# Расширение, добавляющее возможность сообщить модератору о некорректном собщении
#
# Использование:
#  Добавьте в файл ~/.config/tkLOR/userConfig (или создайте его, если он отсутствует)
#  строку
#    loadPlugin "/путь/к/этому/файлу"
#  Путь можно указывать как абсолютный, так и относительно текущего каталога.
#  Обращаться к переменным окружения можно через $env(VARIABLE), например:
#    loadPlugin "$env(HOME)/tklor-addons/reportToModerator"
#
#  Так как тема для некорректных сообщений не является постоянной, то в какой-то
#  момент сообщения перестанут отправляться по причине "нельзя писать в
#  устаревшие темы". Для исправления данной ситуации надо удалить уже отправленные
#  жалюбы из очереди, поменять в этом файле значение переменной donosThread на 
#  актуальное и перезапустить программу.
#  Идентификатор темы можно взять из её URL: например, у темы с адресом 
#    http://www.linux.org.ru/view-message.jsp?msgid=2942017
#  идентификатор равен, соответственно, 2942017.
#

############################################################################
#   Copyright (C) 2008 Alexander Galanin <gaa.nnov@mail.ru>                #
#                                                                          #
#   This program is free software: you can redistribute it and/or modify   #
#   it under the terms of the GNU General Public License as published by   #
#   the Free Software Foundation, either version 3 of the License, or      #
#   (at your option) any later version.                                    #
#                                                                          #
#   This program is distributed in the hope that it will be useful,        #
#   but WITHOUT ANY WARRANTY; without even the implied warranty of         #
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the          #
#   GNU General Public License for more details.                           #
#                                                                          #
#   You should have received a copy of the GNU General Public License      #
#   along with this program.  If not, see <http://www.gnu.org/licenses/>.  #
############################################################################

foreach w [ list $messageTree $topicTree ] {
    bind $w <Control-d> \
        [ list invokeMenuCommand $w ::user::donos::openDonosWindow ]
}

foreach {m w invoke} [ list \
    $menuTopic $topicTree invokeMenuCommand \
    $topicMenu $topicTree invokeItemCommand \
    $menuMessage $messageTree invokeMenuCommand \
    $messageMenu $messageTree invokeItemCommand ] {

    $m add command \
        -label "Донести модератору" \
        -command [ list $invoke $w ::user::donos::openDonosWindow ] \
        -accelerator "Ctrl-D"
}

namespace eval user {
namespace eval donos {

set id 0

set donosThread "2942017"

set reasons {
    {3.1 Дубль}
    {3.2 Неверная кодировка}
    {3.3 Некорректное форматирование}
    {3.4 Пустое сообщение}
    {4.1 Offtopic}
    {4.2 Вызывающе неверная информация}
    {4.3 Провокация flame}
    {4.4 Обсуждение действий модераторов}
    {4.5 Тестовые сообщения}
    {4.6 Спам}
    {4.7 Флуд}
    {5.1 Нецензурные выражения}
    {5.2 Оскорбление участников дискуссии}
    {5.3 Национальные/политические/религиозные споры}
    {5.4 Личная переписка}
    {5.5 Преднамеренное нарушение правил русского языка}
    {6 Нарушение copyright}
    {6.2 Warez}
    {7.1 Ответ на некорректное сообщение}
}

proc openDonosWindow {w item} {
    variable id
    variable reasons
    global currentTopic

    if { $w == $::messageTree } {
        if { $::rightViewState != "MESSAGE" } {
            return
        }
        if { $item == "topic" } {
            set url [ ::lor::getTopicUrl $currentTopic ]
        } else {
            set url [ ::lor::getMessageUrl $item $currentTopic ]
        }
    } else {
        if [ regexp {^\d+$} $item ] {
            set url [ ::lor::getTopicUrl $item ]
        } else {
            return
        }
    }

    set f [ toplevel ".donosWindow[ incr id ]" -class Dialog ]
    wm title $f "Накатать донос"
    wm withdraw $f
    set w [ ttk::frame $f.frame ]
    grid \
        [ ttk::label $w.toLabel -text "Донос на: " ] \
        [ ttk::label $w.toContent -text $url ] \
        -sticky nswe
    grid \
        [ ttk::label $w.reasonLabel -text "Причина: " ] \
        [ ttk::combobox $w.reasonContent -values $reasons ] \
        -sticky nswe
    $w.reasonContent current 11
    grid columnconfigure $w 0 -weight 1
    grid columnconfigure $w 1 -weight 1
    grid $w -sticky nwse

    set donosScript "[ namespace current ]::sendDonos $f;destroy $f"
    set destroyScript "destroy $f"
    grid [ ::gaa::tileDialogs::buttonBox $f \
        [ list -text "Донести" -command $donosScript ] \
        [ list -text "Отставить" -command $destroyScript ] \
    ] -sticky nswe

    grid columnconfigure $f 0 -weight 1
    grid rowconfigure $f 1 -weight 1

    wm transient $f .
    wm deiconify $f
    wm protocol $f WM_DELETE_WINDOW $destroyScript
    bind $f <Escape> $destroyScript
    bind $f <Return> $donosScript

    update
    focus $w.reasonContent
}

proc sendDonos {f} {
    variable donosThread

    set url [ $f.frame.toContent cget -text ]
    set reason [ $f.frame.reasonContent get ]

    putMailToQueue outcoming [ list \
        From        $::lorLogin \
        To          "moderators" \
        Subject     "Робот-доносчик" \
        In-Reply-To $donosThread \
        X-LOR-Time  [ clock format \
                        [ clock seconds ] -format {%d.%m.%Y %H:%M:%S} \
                    ] \
        Content-Type "text/plain" \
        body        "Ссылка: $url\nПричина: $reason" \
    ]
}

}
}

